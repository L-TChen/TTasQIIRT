open import Prelude

module Theory.SC.QIIRT-tyOf.Model where

record Motive (ℓ₁ ℓ₂ ℓ₃ ℓ₄ : Level) : Set (ℓ-suc (ℓ₁ ⊔ ℓ₂ ⊔ ℓ₃ ⊔ ℓ₄)) where
  field
    Ctxᴬ  : Set ℓ₁
    Tyᴬ   : Ctxᴬ → Set ℓ₂
    Subᴬ  : Ctxᴬ → Ctxᴬ → Set ℓ₃
    Tmᴬ   : Ctxᴬ → Set ℓ₄
    tyOfᴬ : {Γᴹ : Ctxᴬ} → Tmᴬ Γᴹ → Tyᴬ Γᴹ

    Tyᴬ-is-set : {Γᴹ : Ctxᴬ} → isSet (Tyᴬ Γᴹ)
    Subᴬ-is-set : {Γᴹ Δᴹ : Ctxᴬ} → isSet (Subᴬ Γᴹ Δᴹ)
    Tmᴬ-is-set : {Γᴹ : Ctxᴬ} → isSet (Tmᴬ Γᴹ)

  variable
    Γᴹ Δᴹ Θᴹ Ξᴹ : Ctxᴬ
    Aᴹ Bᴹ Cᴹ Dᴹ : Tyᴬ Γᴹ
    σᴹ τᴹ γᴹ    : Subᴬ Γᴹ Δᴹ
    tᴹ uᴹ vᴹ    : Tmᴬ Γᴹ

module _ (mot : Motive ℓ₁ ℓ₂ ℓ₃ ℓ₄) where
  open Motive mot

  record SCᴹ : Set (ℓ₁ ⊔ ℓ₂ ⊔ ℓ₃ ⊔ ℓ₄) where
    field
      ∅ᴹ
        : Ctxᴬ
      _,ᴹ_
        : (Γᴹ : Ctxᴬ)(Aᴹ : Tyᴬ Γᴹ)
        → Ctxᴬ
      _[_]Tᴹ
        : (Aᴹ : Tyᴬ Δᴹ)(σᴹ : Subᴬ Γᴹ Δᴹ)
        → Tyᴬ Γᴹ
      _[_]tᴹ
        : (tᴹ : Tmᴬ Δᴹ)(σᴹ : Subᴬ Γᴹ Δᴹ)
        → Tmᴬ Γᴹ
      tyOf[]ᴹ
        : tyOfᴬ (tᴹ [ σᴹ ]tᴹ) ≡ (tyOfᴬ tᴹ) [ σᴹ ]Tᴹ
      ∅Sᴹ
        : Subᴬ Γᴹ ∅ᴹ
      _,ᴹ_∶[_]
        : (σᴹ : Subᴬ Γᴹ Δᴹ) (tᴹ : Tmᴬ Γᴹ) → tyOfᴬ tᴹ ≡ Aᴹ [ σᴹ ]Tᴹ
        → Subᴬ Γᴹ (Δᴹ ,ᴹ Aᴹ)
      idSᴹ
        : Subᴬ Γᴹ Γᴹ
      _∘ᴹ_
        : Subᴬ Δᴹ Θᴹ → Subᴬ Γᴹ Δᴹ
        → Subᴬ Γᴹ Θᴹ
      π₁ᴹ
        : Subᴬ Γᴹ (Δᴹ ,ᴹ Aᴹ)
        → Subᴬ Γᴹ Δᴹ
      π₂ᴹ
        : Subᴬ Γᴹ (Δᴹ ,ᴹ Aᴹ)
        → Tmᴬ Γᴹ
      tyOfπ₂ᴹ
        : {Aᴹ : Tyᴬ Δᴹ} (σᴹ : Subᴬ Γᴹ (Δᴹ ,ᴹ Aᴹ))
        → tyOfᴬ (π₂ᴹ {Aᴹ = Aᴹ} σᴹ) ≡ Aᴹ [ π₁ᴹ σᴹ ]Tᴹ
      idS∘ᴹ_
        : (σᴹ : Subᴬ Γᴹ Δᴹ)
        → idSᴹ ∘ᴹ σᴹ ≡ σᴹ
      _∘idSᴹ
        : (σᴹ : Subᴬ Γᴹ Δᴹ)
        → σᴹ ∘ᴹ idSᴹ ≡ σᴹ
      assocSᴹ
        : (σᴹ : Subᴬ Γᴹ Δᴹ) (τᴹ : Subᴬ Δᴹ Θᴹ) (γᴹ : Subᴬ Θᴹ Ξᴹ)
        → (γᴹ ∘ᴹ τᴹ) ∘ᴹ σᴹ ≡ γᴹ ∘ᴹ (τᴹ ∘ᴹ σᴹ)
      [idS]Tᴹ
        : Aᴹ ≡ Aᴹ [ idSᴹ ]Tᴹ
      [∘]Tᴹ
        : (Aᴹ : Tyᴬ Θᴹ) (σᴹ : Subᴬ Γᴹ Δᴹ) (τᴹ : Subᴬ Δᴹ Θᴹ)
        → Aᴹ [ τᴹ ]Tᴹ [ σᴹ ]Tᴹ ≡ Aᴹ [ τᴹ ∘ᴹ σᴹ ]Tᴹ
      ,∘ᴹ
        : (σᴹ : Subᴬ Δᴹ Θᴹ) (tᴹ : Tmᴬ Δᴹ) (τᴹ : Subᴬ Γᴹ Δᴹ)
        → (p : tyOfᴬ tᴹ ≡ Aᴹ [ σᴹ ]Tᴹ) (q : tyOfᴬ (tᴹ [ τᴹ ]tᴹ) ≡ Aᴹ [ σᴹ ∘ᴹ τᴹ ]Tᴹ)
--        → let q = tyOfᴬ (tᴹ [ τᴹ ]tᴹ)
--                    ≡⟨ tyOf[]ᴹ ⟩
--                  (tyOfᴬ tᴹ) [ τᴹ ]Tᴹ
--                    ≡[ i ]⟨ p i [ τᴹ ]Tᴹ ⟩
--                  Aᴹ [ σᴹ ]Tᴹ [ τᴹ ]Tᴹ
--                    ≡⟨ [∘]Tᴹ _ _ _ ⟩
--                  Aᴹ [ σᴹ ∘ᴹ τᴹ ]Tᴹ
--                    ∎
--        in
        → (σᴹ ,ᴹ tᴹ ∶[ p ]) ∘ᴹ τᴹ ≡ ((σᴹ ∘ᴹ τᴹ) ,ᴹ tᴹ [ τᴹ ]tᴹ ∶[ q ])
      ηπᴹ
        : (σᴹ : Subᴬ Γᴹ (Δᴹ ,ᴹ Aᴹ))
        → σᴹ ≡ (π₁ᴹ σᴹ ,ᴹ π₂ᴹ σᴹ ∶[ tyOfπ₂ᴹ _ ])
      η∅ᴹ
        : (σᴹ : Subᴬ Γᴹ ∅ᴹ)
        → σᴹ ≡ ∅Sᴹ
      βπ₁ᴹ
        : (σᴹ : Subᴬ Γᴹ Δᴹ) (tᴹ : Tmᴬ Γᴹ) (p : tyOfᴬ tᴹ ≡ Aᴹ [ σᴹ ]Tᴹ)
        → π₁ᴹ (σᴹ ,ᴹ tᴹ ∶[ p ]) ≡ σᴹ
      βπ₂ᴹ
        : (σᴹ : Subᴬ Γᴹ Δᴹ) (tᴹ : Tmᴬ Γᴹ) (p : tyOfᴬ tᴹ ≡ Aᴹ [ σᴹ ]Tᴹ)
--        → (q : Aᴹ [ π₁ᴹ (σᴹ ,ᴹ tᴹ ∶[ p ]) ]Tᴹ ≡  tyOfᴬ tᴹ)
        → π₂ᴹ (σᴹ ,ᴹ tᴹ ∶[ p ]) ≡ tᴹ
      [idS]tᴹ
        : (tᴹ : Tmᴬ Γᴹ)
        → tᴹ ≡ tᴹ [ idSᴹ ]tᴹ
      [∘]tᴹ
        : (tᴹ : Tmᴬ Θᴹ) (σᴹ : Subᴬ Γᴹ Δᴹ) (τᴹ : Subᴬ Δᴹ Θᴹ)
        → tᴹ [ τᴹ ]tᴹ [ σᴹ ]tᴹ ≡ tᴹ [ τᴹ ∘ᴹ σᴹ ]tᴹ
      Uᴹ
        : Tyᴬ Γᴹ
      U[]ᴹ
        : Uᴹ [ σᴹ ]Tᴹ ≡ Uᴹ

    
    tyOfπ₂[]ᴹ
      : (τᴹ : Subᴬ Δᴹ (Θᴹ ,ᴹ Aᴹ))
      → (σᴹ : Subᴬ Γᴹ Δᴹ)
      → tyOfᴬ (π₂ᴹ τᴹ [ σᴹ ]tᴹ) ≡ Aᴹ [ π₁ᴹ τᴹ ∘ᴹ σᴹ ]Tᴹ
    tyOfπ₂[]ᴹ {Δᴹ} {Θᴹ} {Aᴹ} {_} τᴹ σᴹ = tyOf[]ᴹ ∙ (λ i → tyOfπ₂ᴹ τᴹ i [ σᴹ ]Tᴹ) ∙ [∘]Tᴹ Aᴹ σᴹ (π₁ᴹ τᴹ)
--      tyOfᴬ (π₂ᴹ τᴹ [ σᴹ ]tᴹ)
--        ≡⟨ tyOf[]ᴹ ⟩
--      tyOfᴬ (π₂ᴹ τᴹ) [ σᴹ ]Tᴹ
--        ≡[ i ]⟨ tyOfπ₂ᴹ τᴹ i [ σᴹ ]Tᴹ ⟩
--      Aᴹ [ π₁ᴹ τᴹ ]Tᴹ [ σᴹ ]Tᴹ
--        ≡⟨ [∘]Tᴹ Aᴹ σᴹ (π₁ᴹ τᴹ) ⟩
--      Aᴹ [ π₁ᴹ τᴹ ∘ᴹ σᴹ ]Tᴹ
--        ∎
        
    π₁∘ᴹ
      : (τᴹ : Subᴬ Δᴹ (Θᴹ ,ᴹ Aᴹ)) (σᴹ : Subᴬ Γᴹ Δᴹ)
      → π₁ᴹ (τᴹ ∘ᴹ σᴹ) ≡ π₁ᴹ τᴹ ∘ᴹ σᴹ
    π₁∘ᴹ {Aᴹ = Aᴹ} τᴹ σᴹ =
      π₁ᴹ (τᴹ ∘ᴹ σᴹ)
        ≡⟨ cong π₁ᴹ (cong (_∘ᴹ σᴹ) (ηπᴹ τᴹ)) ⟩
      π₁ᴹ ((π₁ᴹ τᴹ ,ᴹ π₂ᴹ τᴹ ∶[ tyOfπ₂ᴹ τᴹ ]) ∘ᴹ σᴹ)
        ≡⟨ cong π₁ᴹ (,∘ᴹ (π₁ᴹ τᴹ) (π₂ᴹ τᴹ) σᴹ (tyOfπ₂ᴹ τᴹ) (tyOfπ₂[]ᴹ τᴹ σᴹ)) ⟩
      π₁ᴹ ((π₁ᴹ τᴹ ∘ᴹ σᴹ) ,ᴹ (π₂ᴹ τᴹ [ σᴹ ]tᴹ) ∶[ _ ])
        ≡⟨  βπ₁ᴹ (π₁ᴹ τᴹ ∘ᴹ σᴹ) (π₂ᴹ τᴹ [ σᴹ ]tᴹ)
          (tyOf[]ᴹ ∙ cong _[ σᴹ ]Tᴹ (tyOfπ₂ᴹ τᴹ) ∙ [∘]Tᴹ _ _ _)  ⟩
      π₁ᴹ τᴹ ∘ᴹ σᴹ
        ∎

    π₂∘ᴹ
      : (τᴹ : Subᴬ Δᴹ (Θᴹ ,ᴹ Aᴹ)) (σᴹ : Subᴬ Γᴹ Δᴹ)
      → π₂ᴹ (τᴹ ∘ᴹ σᴹ) ≡ π₂ᴹ τᴹ [ σᴹ ]tᴹ
    π₂∘ᴹ τᴹ σᴹ =
      π₂ᴹ (τᴹ ∘ᴹ σᴹ)
        ≡[ i ]⟨ π₂ᴹ (ηπᴹ τᴹ i ∘ᴹ σᴹ) ⟩
      π₂ᴹ ((π₁ᴹ τᴹ ,ᴹ π₂ᴹ τᴹ ∶[ _ ]) ∘ᴹ σᴹ)
        ≡[ i ]⟨ π₂ᴹ (,∘ᴹ (π₁ᴹ τᴹ) (π₂ᴹ τᴹ) σᴹ (tyOfπ₂ᴹ τᴹ) (tyOfπ₂[]ᴹ τᴹ σᴹ) i) ⟩
      π₂ᴹ ((π₁ᴹ τᴹ ∘ᴹ σᴹ) ,ᴹ (π₂ᴹ τᴹ [ σᴹ ]tᴹ) ∶[ _ ])
        ≡⟨ βπ₂ᴹ _ _ _ ⟩
      π₂ᴹ τᴹ [ σᴹ ]tᴹ
        ∎

    π₁σ=π₁idS∘σᴹ
      : (σᴹ : Subᴬ Γᴹ (Δᴹ ,ᴹ Aᴹ))
      → π₁ᴹ σᴹ ≡ π₁ᴹ idSᴹ ∘ᴹ σᴹ
    π₁σ=π₁idS∘σᴹ σᴹ =
      π₁ᴹ σᴹ
        ≡[ i ]⟨ π₁ᴹ ((idS∘ᴹ σᴹ) (~ i)) ⟩
      π₁ᴹ (idSᴹ ∘ᴹ σᴹ)
        ≡⟨ π₁∘ᴹ idSᴹ σᴹ ⟩
      π₁ᴹ idSᴹ ∘ᴹ σᴹ
        ∎
    
    π₂σ=π₂id[σ]ᴹ
      : (σᴹ : Subᴬ Γᴹ (Δᴹ ,ᴹ Aᴹ))
      → π₂ᴹ σᴹ ≡ π₂ᴹ idSᴹ [ σᴹ ]tᴹ
    π₂σ=π₂id[σ]ᴹ σᴹ =
      π₂ᴹ σᴹ
        ≡[ i ]⟨ π₂ᴹ ((idS∘ᴹ σᴹ) (~ i)) ⟩
      π₂ᴹ (idSᴹ ∘ᴹ σᴹ)
        ≡⟨ π₂∘ᴹ idSᴹ σᴹ ⟩
      π₂ᴹ idSᴹ [ σᴹ ]tᴹ
        ∎
    
    cong,∶[]ᴹ
      : {Aᴹ : Tyᴬ Δᴹ}{σᴹ σ'ᴹ : Subᴬ Γᴹ Δᴹ}{tᴹ t'ᴹ : Tmᴬ Γᴹ}
      → (p : tyOfᴬ tᴹ ≡ Aᴹ [ σᴹ ]Tᴹ)(p' : tyOfᴬ t'ᴹ ≡ Aᴹ [ σ'ᴹ ]Tᴹ)
      → σᴹ ≡ σ'ᴹ → tᴹ ≡ t'ᴹ
      → (σᴹ ,ᴹ tᴹ ∶[ p ]) ≡ (σ'ᴹ ,ᴹ t'ᴹ ∶[ p' ])
    cong,∶[]ᴹ {Aᴹ = Aᴹ} p p' eqσ eqt =
      cong₃ _,ᴹ_∶[_] eqσ eqt (isSet→SquareP (λ _ _ → Tyᴬ-is-set) p p' (cong tyOfᴬ eqt) (cong (Aᴹ [_]Tᴹ) eqσ))
